#################################################################################
# Airport Example #2
# Gian Perrone, August 2011
#
# This is a very crude model of an airport with two passengers.
# Passengers travel to their flights through a series of steps,
# and enter a gate based upon their associated record in the
# passenger database.
############################## Definitions Section ##############################
# We distinguish landside and airside

controls:
	active Landside  0
	active Airside   0
	active Gates 	 0

# We distinguish zone types
# The two links are [identifier, exit]

	active Zone 	 2

# A gate is simply linked to a flight
	active Gate 	 1

# A passenger is linked to a flight
	passive Pax 	 1

# The database store
	active DB 		 0
	active PaxRecord 2 	# Links [passenger,gate]

# Names that represent zones
names:
	outer CheckIn
	outer Security
	outer DutyFree	
	outer GateLounge

# A gate status at the departure lounge
	outer Boarding

# Some flights
	outer SK100
	outer SK101
	outer SK102

# Example passengers
	outer Bob
	outer Joe
	outer Jane
	outer Mary

############################## Reaction Rules ####################################

# Rule that allow passengers to move through the airport
rule movementAirport =
	Zone[@w,@x].(Pax[@y] | $0) || Zone[@x,@z].$1 -> 
	Zone[@w,@x].$0 || Zone[@x,@z].(Pax[@y] | $1)


# A passenger linked to a PaxRecord can proceed to the gate
rule linkedPassenger =
Zone[@m,Boarding].(Pax[@y] | $0) || Gate[@x].$1 || PaxRecord[@y,@x] | $2 ->
	Zone[@m,Boarding].$0 || Gate[@x].(Pax[@y] | $1) || PaxRecord[@y,@x] | $2

# A passenger magically disappears once they board the aircraft
rule disappear = Gate[@x].(Pax[@y] | $0) -> Gate[@x].$0

############################## Airport Model  ####################################

model airport2 =

Landside.(
	Zone[-,CheckIn].(Pax[Bob] | Pax[Joe] | Pax[Jane] | Pax[Mary]) | 
	Zone[CheckIn,Security]
) | 
Airside.(
	Zone[Security,DutyFree] | 
	Zone[DutyFree,GateLounge] |
	Zone[GateLounge,Boarding]
) |
Gates.(
	Gate[SK100] | 
	Gate[SK102] |
	Gate[SK101]
) | DB.(
	PaxRecord[Joe,SK100] |
	PaxRecord[Bob,SK101] |
    PaxRecord[Jane,SK100]
)